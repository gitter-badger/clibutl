<pass0>=@enm{$r}\n\#include "peg.h"\n\n@hdr{$r}\n@pass1{$1}
!<pass0>=@write{pegout.c;@hdr{$r}\n@enm{$r}@pass1{$1}}@close{pegout.c}
!<pass0>=@write{pegout_t.c;@hdr{$r}\n$1}@close{pegout_t.c}


!-- PASS 1 ---
pass0:\A=@set{n;0}@set{x;}@set{r;}
pass0:\n<s>=\n
pass0:\#*\r\n\W=\n
pass0:\#*\n\W=\n
pass0:\W<I>\W\:\=\W<expr0>=\#$l\n$1\s\:\=\s$2\;$x@set{x;}\n@append{r;$1\n}
pass0:---*=

expr0:\A=@set{l;@sub{@line;1}}
expr0:\[<cls0>\]=[$1]
expr0:"<str>"="$1"
expr0:'<str>'="$1"
expr0:\W\/\W=\s\/\s
expr0:(#)=_$n@append{x;\n_$n \:\= # \;}@append{r;peg__$n\n}@incr{n}
expr0:\#*\P\r\n=
expr0:\#*\P\n=
expr0:\W\n\W=\s
expr0:\;=@terminate
expr0:\P\W<I>\W\:\==@terminate
expr0:---*=@terminate

hdr:<I>=int peg_$1(peg p)\;

enm:\A=typedef enum \{
enm:<I>=peg__id_$1,
enm:\Z=\} peg__id\;\n

cls0:\\<U>=\\$1
cls0:"=\\"

!-- PASS 1 ---

!pass1:\#<D>=\#line $1 "@inpath{}"@set{l;$1}
pass1:\#<D>=
pass1:<I>\W\:\=\W<expr1>=int peg_$1(peg p) \{\n\
                            unsigned long q,qq\;\n\
                            if (peg__check(p,peg__id_$1)) return peg__success(p)\;\n\
                            q=peg__save(p)\;\n\
                            $2\n\
                            return peg__fail(p)\;\n\
                        \}\;\n

expr1:<term>=\s\sdo \{\n$1 \n\s\sreturn peg__success(p)\;\n\s\s\}while(0)\;\n\s\speg__back(p,q)\;\n
expr1:\;=@terminate

term:\!<atom>=qq = peg__save(p)\; if ($1) break\; peg__back(p,q)\;
term:\&<atom>=qq = peg__save(p)\; if (!$1) break\; peg__back(p,q)\;
term:<atom><opt>=@code{$2 $1}
term:\/=@terminate
term:\P\;=@terminate

atom:\[<cls1>\]=peg__class(p,"$1")@terminate
atom:"<str>"=peg__string(p,"$1")@terminate
atom:<I>=peg_$1(p)@terminate
atom:.=peg__any(p)@terminate
atom:=@terminate

opt:\*=S@terminate
opt:\?=Q@terminate
opt:\+=P@terminate
opt:=N@terminate

code:S *=while($1)\;\n@terminate
code:P *=if (\!$1) break\;\nwhile($1)\;\n@terminate
code:N *=if (\!$1) break\;\n@terminate
code:Q *=$1\;\n@terminate

cls1:\\\]=]

!-- MISC ---
str:\\<U>=\\$1


